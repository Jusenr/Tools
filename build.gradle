// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "config.gradle"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

        //maven
        classpath 'com.novoda:bintray-release:0.4.0'

        //maven 2
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
//        classpath 'com.github.dcendents:android-maven-plugin:1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        //AAR local library dependencies
        flatDir {
            dirs 'libs'
        }
    }
    //Set unified encoding
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    //Setting unified javaDoc encoding
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
        options.addStringOption('link', 'http://docs.oracle.com/javase/7/docs/api')
        options.addStringOption('windowtitle', 'androidtools')
        options.addBooleanOption('author', true)
        options.addBooleanOption('version', true)
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
//            force 'com.android.support:support-v4:23.0.1'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//Get version number
def getVersionName() {
    int num = rootProject.ext.VERSION_NUM
    if (num > 0) {
        return String.format("%s.%d", project.properties["PROJ_VERSIONNAME"], rootProject.ext.VERSION_NUM)
    } else {
        return project.properties["PROJ_VERSIONNAME"]
    }
}

//Git commit times
int gitVersionCode() {
    if (System.getenv("CI_BUILD")) {
        return rootProject.ext.androids.versionCode
    }
    def count = "git rev-list HEAD --count".execute().text.trim()
    return count.isInteger() ? count.toInteger() : 0
}

//Get current time
def releaseTime(def format) {
    return new Date().format(format)
}

//VersionCode is reassigned to the number of GIT commit
rootProject.ext.androids.versionCode = gitVersionCode()